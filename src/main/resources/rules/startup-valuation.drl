/*
 * Copyright 2015 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
*/


import cz.utb.fai.dto.StartupValuationRes
import cz.utb.fai.dto.type.Evidence
import cz.utb.fai.dto.type.Hypothesis
import cz.utb.fai.dto.type.Probability
import java.util.ArrayList
import java.util.List
import java.util.Arrays
import java.util.Collections
import cz.utb.fai.drools.TrackingAgendaEventListener
import cz.utb.fai.dto.type.Score
import cz.utb.fai.dto.type.Conclusion
import org.kie.api.runtime.rule.Agenda
import cz.utb.fai.dto.type.Probability

global Conclusion response
global String YES


rule "Business Needs - No"
salience 100
when
    Evidence(description == "Do you know what your business needs?", value == "no")
    Evidence(description == "What is your Technology Readiness Level?", value == $value : value)
then
    response.appendExplanation(new Hypothesis("Your business needs further analysis").getDescription());
    double rate = TrackingAgendaEventListener.adjustProductScore(Score.PRODUCT_SCORE, $value);
    response.setRate(rate);
    response.appendExplanation(new Hypothesis("Your product has a Technology Readiness Level of " + $value + " out of 9.").getDescription());
    response.appendExplanation(new Hypothesis("You current success probability is " + rate + " out of 100.").getDescription());
end

rule "Business Needs - Yes"
salience 99
when
    Evidence(description == "Do you know what your business needs?", value == "yes")
then
    insertLogical(new Hypothesis("You may know your business needs."));
end

rule "Marketing skill"
salience 94
when
    Evidence(description == "Do you have the marketing skills?", value == $ms : value )
then
    double rate = $ms.equals(YES) ? Score.MARKETING_SKILLS : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("Consider expanding your marketing skills.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end

rule "Technical skill"
salience 93
when
    Evidence(description == "Do you have the technical skills?", value == $ts : value )
then
    double rate = $ts.equals(YES) ? Score.TECHNICAL_SKILLS : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("Consider expanding your technical skills.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end

rule "Analytical skill"
salience 92
when
    Evidence(description == "Do you have the analytical skills?", value == $as : value )
then
    double rate = $as.equals(YES) ? Score.ANALYTICAL_SKILLS : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("Consider expanding your analytical skills.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end

rule "Goal"
salience 91
when
    Evidence(description == "Does your company have clear goals that align with your team members values?", value == $goal : value )
then
    double rate = $goal.equals(YES) ? Score.TEAM_SKILLS : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("Consider reassessing whether your goals align with the company's vision.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end

rule "Experience"
salience 90
when
    Evidence(description == "Does your team have previous experience working together?", value == $experience : value )
then
    double rate = $experience.equals(YES) ? Score.TEAM_SKILLS : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("Consider hiring a team with previous experience to enhance collaboration and efficiency.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end

rule "Expertise"
salience 89
when
    Evidence(description == "Does your team have insider expertise or external help in the sector of your business?", value == $expertise : value )
then
    double rate = $expertise.equals(YES) ? Score.TEAM_SKILLS : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("It may be beneficial to prioritize expertise in the field to enhance your team's capabilities.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end


rule "Product Development"
salience 80
when
    Evidence(description == "Do you know what your business needs?", value == "yes")
    Evidence(description == "What is your Technology Readiness Level?", value == $value : value)
then
    double rate = TrackingAgendaEventListener.adjustProductScore(Score.PRODUCT_SCORE, $value);
    Hypothesis hypothesis = new Hypothesis("Your product has a Technology Readiness Level of " + $value + " out of 9.");

    response.appendRate(rate);
    response.appendExplanation(hypothesis.getDescription());
end

rule "Intellectual Property (IP)"
salience 75
when
    Evidence(description == "Do you have competitors?", value == "yes")
    Evidence(description == "Can you protect your product/service?", value == $value : value)
then
    if($value.equals(YES)) {
        response.appendRate(Score.IP_SCORE);
    } else {
        response.appendExplanation(new Hypothesis("Considering intellectual property protection for your product/service could be advantageous.").getDescription());
    }
end

rule "Traction"
salience 70
when
    Evidence(description == "What's the lifetime value of your potential customer?", value == $ltv : value)
    Evidence(description == "What's the potential customer acquisition cost?", value == $cac : value)
    Evidence(description == "From 1 to 10, how good is your engagement rate?", value == $interested : value)
then
    double ltvToCac = TrackingAgendaEventListener.parseLtvAndCac(Double.parseDouble($ltv), Double.parseDouble($cac));
    if (ltvToCac < 3) {
        response.appendExplanation(new Hypothesis("On average you are not generating revenue.").getDescription());
    }
    insertLogical(new Probability(ltvToCac * 0.1, "P(Traction|LTV:CAC)"));
    insertLogical(new Probability(Double.parseDouble($interested) * 0.1, "P(Traction|Interested)"));
end

rule "P(Traction|LTV:CAC) and P(Traction|Interested)"
@LS1(9.0)
@LN1(0.67)
@LS2(1.5)
@LN2(0.11)
salience 60
when
    exists Probability(description == "P(Traction|LTV:CAC)")
    exists Probability(description == "P(Traction|Interested)")
then
    TrackingAgendaEventListener.getProbabilityRef(Probability.class, "P(Traction|LTV:CAC)").updateProbability();
    TrackingAgendaEventListener.getProbabilityRef(Probability.class, "P(Traction|Interested)").updateProbability();
end

rule "P(Traction|LTV:CAC|Interested)"
salience 55
when
    $LTVtoCAC : Probability(description == "P(Traction|LTV:CAC)")
    $interested : Probability(description == "P(Traction|Interested)")
then
    double probability = $LTVtoCAC.getProbability() * $interested.getProbability();
    response.appendRate(probability * Score.TRACTION_SCORE);
end

rule "Strategic Investor"
salience 35
when
    Evidence(description == "Do you have a strategic investor?", value == $investor : value)
then
    double rate = $investor.equals(YES) ? Score.INVESTOR_SCORE : Score.NEGATIVE_SCENARIO;
    if (rate == Score.NEGATIVE_SCENARIO) {
        Hypothesis hypothesis = new Hypothesis("Considering the potential benefits, you may want to explore opportunities to secure a strategic investor.");
        response.appendExplanation(hypothesis.getDescription());
    }
    response.appendRate(rate);
end

rule "Opportunity"
salience 30
when
    Evidence(description == "What’s your SAM within 5 years?", value == $sam : value)
    Evidence(description == "What’s your TAM within 5 years?", value == $tam : value)
    Evidence(description == "How much money do you need to enter a market?", value == $money : value)
then
    double samToMoney = TrackingAgendaEventListener.parseSamToMoney(Double.parseDouble($sam), Double.parseDouble($money));
    double tamToSam = TrackingAgendaEventListener.parseTamToSam(Double.parseDouble($tam), Double.parseDouble($sam));
    if (samToMoney < 8) {
        response.appendExplanation(new Hypothesis("Your investment return is expected to yield less than a 10x ROI.").getDescription());
    }
    insertLogical(new Probability(samToMoney * 0.1, "P(Opportunity|SAM:Money)"));
    insertLogical(new Probability(samToMoney * 0.1, "P(Opportunity|TAM:SAM)"));
end

rule "P(Opportunity|SAM:Money) and P(Opportunity|TAM:SAM)"
@LS1(9.0)
@LN1(1.0)
@LS2(0.67)
@LN2(0.11)
salience 25
when
    exists Probability(description == "P(Opportunity|SAM:Money)")
    exists Probability(description == "P(Opportunity|TAM:SAM)")
then
    TrackingAgendaEventListener.getProbabilityRef(Probability.class, "P(Opportunity|SAM:Money)").updateProbability();
    TrackingAgendaEventListener.getProbabilityRef(Probability.class, "P(Opportunity|TAM:SAM)").updateProbability();
end

rule "P(Opportunity|SAM:Money|TAM:SAM)"
salience 20
when
    $samToMoney : Probability(description == "P(Opportunity|SAM:Money)")
    $tamToSam : Probability(description == "P(Opportunity|TAM:SAM)")
then
    double probability = $samToMoney.getProbability() * $tamToSam.getProbability();
    response.appendRate(probability * Score.OPPORTUNITY_SCORE);
    response.appendExplanation(new Hypothesis("You current success probability is " + response.getRate() + " out of 100.").getDescription());
end